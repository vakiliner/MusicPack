plugins {
	id 'net.minecraftforge.gradle' version '[6.0,6.2)'
	id 'org.spongepowered.mixin' version '0.7.+'
}

minecraft {
	mappings channel: mapping_channel, version: minecraft_version

	runs {
		configureEach {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'

			mods {
				musicpack {
					source sourceSets.main
				}
			}
		}

		client {
		}

		server {
			args '--nogui'
		}

		data {
			workingDirectory project.file('run-data')

			args '--mod', 'musicpack', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	implementation project(':base')
	minecraft group: 'net.minecraftforge', name: 'forge', version: "${project.minecraft_version}-${project.forge_version}"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
	add sourceSets.main, "${project.rootProject.base.archivesName.get()}-forge-refmap.json"
}

processResources {
	var replaceProperties = [
		version: project.version,
		license: project.license,
	]
	inputs.properties replaceProperties

	filesMatching('META-INF/mods.toml') {
		expand replaceProperties
	}
}

jar {
	var task = project(':base').tasks.getByName('jar')

	from(zipTree(task.archiveFile)) {
		exclude 'LICENSE'
	}

	manifest {
		from task.manifest

		attributes([
			MixinConfigs: 'mixins.musicpack.forge.json'
		])
	}
}
